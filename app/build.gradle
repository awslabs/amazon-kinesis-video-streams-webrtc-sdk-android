plugins {
    id 'com.android.application'
    id 'jacoco'
}

android {
    compileSdk 33

    defaultConfig {
        namespace("com.amazonaws.kinesisvideo.demoapp")

        applicationId "com.amazonaws.kinesisvideo.webrtc_sdk_android"
        minSdk 29
        targetSdk 33
        versionCode 1
        versionName "1.1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        
        // Load .env file and set BuildConfig fields
        def envFile = rootProject.file('.env')
        def envFields = [:]
        if (envFile.exists()) {
            envFile.readLines().each {
                def (key, value) = it.tokenize('=')
                if (key && value) {
                    envFields[key] = value
                    buildConfigField "String", key, "\"${value}\""
                }
            }
        }
        
        // Set default null values for missing fields
        if (!envFields.containsKey('CUSTOM_ENDPOINT')) {
            buildConfigField "String", "CUSTOM_ENDPOINT", "null"
        }
        if (!envFields.containsKey('AWS_ACCESS_KEY_ID')) {
            buildConfigField "String", "AWS_ACCESS_KEY_ID", "null"
        }
        if (!envFields.containsKey('AWS_SECRET_ACCESS_KEY')) {
            buildConfigField "String", "AWS_SECRET_ACCESS_KEY", "null"
        }
        if (!envFields.containsKey('AWS_SESSION_TOKEN')) {
            buildConfigField "String", "AWS_SESSION_TOKEN", "null"
        }
    }

    buildFeatures {
        buildConfig true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])

    def aws_version = '2.75.0'
    implementation("com.amazonaws:aws-android-sdk-kinesisvideo:$aws_version@aar") { transitive = true }
    implementation("com.amazonaws:aws-android-sdk-kinesisvideo-signaling:$aws_version@aar") { transitive = true }
    implementation("com.amazonaws:aws-android-sdk-kinesisvideo-webrtcstorage:$aws_version@aar") { transitive = true }
    implementation("com.amazonaws:aws-android-sdk-mobile-client:$aws_version@aar") { transitive = true }
    implementation("com.amazonaws:aws-android-sdk-auth-userpools:$aws_version@aar") { transitive = true }
    implementation("com.amazonaws:aws-android-sdk-auth-ui:$aws_version@aar") { transitive = true }

    implementation 'org.awaitility:awaitility:4.2.0'
    implementation 'org.json:json:20190722'
    implementation 'com.google.guava:guava:28.1-android'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'org.apache.commons:commons-lang3:3.9'
    implementation("com.squareup.okhttp3:okhttp:4.12.0")

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

// Check that libwebrtc is present during build
tasks.register('checkForLibWebRTC') {
    doLast {
        boolean webrtcFound = false

        // Check dependencies by name
        configurations.implementation.dependencies.forEach(dependency -> {
            // Check the name of the dependency, and also the name of the group
            if ((dependency.group && dependency.group.contains("webrtc") || dependency.name.contains("webrtc"))
                    && !dependency.name.contains("webrtcstorage")) {
                webrtcFound = true
            }
        })

        // Check file dependencies
        if (!webrtcFound) {
            fileTree(dir: 'libs', include: ['*.jar', '*.aar']).forEach(file -> {
                if (file.name.contains("webrtc")) {
                    webrtcFound = true
                }
            })
        }

        if (!webrtcFound) {
            throw new RuntimeException(
                    "Unable to find Google WebRTC. You will need to either: (1) Build libwebrtc on " +
                    "your own following directions here https://webrtc.github.io/webrtc-org/native-code/android/ " +
                    "to create the AAR, and place it in the app/libs folder so they can be properly referenced in the " +
                    "build.gradle file or (2) Reference another public repository which vends the android binaries."
            )
        }
    }
}

preBuild.dependsOn checkForLibWebRTC